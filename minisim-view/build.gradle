plugins {
	id 'application'
	id 'com.diffplug.spotless' version '6.11.0'
	id 'org.openjfx.javafxplugin' version '0.0.13'
	id "com.github.spotbugs" version "5.0.12"
	id 'pmd'

	// For .exe creation
	id 'edu.sc.seis.launch4j' version '2.5.3'
}

sourceCompatibility = '17'
def mainClassFullName = 'org.minisim.App'

dependencies {
	implementation project(':minisim-model')
	//implementation 'com.google.guava:guava:31.0.1-jre'
	implementation 'org.jcodec:jcodec-javase:0.2.5' // video encoding compatible with AWT
}

application {
	mainClass = mainClassFullName
}

// Format code always before compiling
tasks.compileJava.dependsOn(spotlessApply)

javafx {
	version = '18.0.2'
	modules = [ 'javafx.controls', 'javafx.graphics' ]
}

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
	//ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
		setEncoding('utf-8')
	}

	java {
		importOrder()
		removeUnusedImports()
		formatAnnotations()
		palantirJavaFormat('2.26.0')
	}
}

launch4j {
	mainClassName = mainClassFullName
	headerType = 'console'
	icon = '${projectDir}/src/main/resources/icons/atom.ico'
	outfile = 'MiniSim.exe'
	windowTitle = 'MiniSim'
	productName = 'MiniSim'
	fileDescription = 'MiniSim'
	language = 'ENGLISH_US'
	//bundledJrePath = '/Users/user/Downloads/jdk' // TODO solve problem of bundling JRE into exe file
}

pmd {
	consoleOutput = true
	toolVersion = "6.21.0"
	rulesMinimumPriority = 5
	ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml"]
	//threads = 4
}
